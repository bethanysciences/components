https://github.com/evsc/ThunderAndLightning

//
// Decide whether to send output to serial or to the bridge
//
//#define YUN_CONSOLE
#ifdef YUN_CONSOLE
#include <Console.h>
#define emit(s) Console.print(s);
#define emitb(s) Console.print(s,BIN);
#define emith(s) Serial.print("0x"); Console.print(s,HEX);
#define emitln(s) Console.println(s);
#else
#define emit(s) Serial.print(s);
#define emitb(s) Serial.print(s,BIN);
#define emith(s) Serial.print("0x"); Serial.print(s,HEX);
#define emitln(s) Serial.println(s);
#endif

//
// Wiring
//
// SCL to analog 3
// SDA to analog 2
// VDD to 5V DC
// GND to ground
// INT to digital 9
//

//
// Configure SoftI2C
// http://playground.arduino.cc/Main/SoftwareI2CLibrary (necessary for repeated start condition)
// https://github.com/felias-fogg/SoftI2CMaster
//
#define SCL_PORT PORTF  // YUN A3
#define SCL_PIN 4
#define SDA_PORT PORTF  // YUN A2
#define SDA_PIN 5
#define INT_PIN 9       // YUN D9
#include <SoftI2CMaster.h>

//
// AS3935
//
#define AS3935_ADDRESS              (0x03 << 1) // convert 7-bit address to 8-bit
#define AS3935_TUNE                 (0x04) // from the package label
//
#define AS3935_DIRECT_COMMAND_BYTE  (0x96)
#define AS3935_AFE_GB_INDOOR        (B00100100)
#define AS3935_AFE_GB_OUTDOOR       (B00011100)
//
#define AS3935_REG_AFE_GB           (0x00)
#define AS3935_REG_PW               (0x00)
#define AS3935_REG_NF_LEV           (0x01)
#define AS3935_REG_WDTH             (0x01)
#define AS3935_REG_CL_STAT          (0x02)
#define AS3935_REG_MIN_NUM_LIGH     (0x02)
#define AS3935_REG_SREJ             (0x02)
#define AS3935_REG_LCO_FDIV         (0x03)
#define AS3935_REG_MASK_DIST        (0x03)
#define AS3935_REG_INT              (0x03)
#define AS3935_REG_S_LIG_L          (0x04)
#define AS3935_REG_S_LIG_M          (0x05)
#define AS3935_REG_S_LIG_MM         (0x06)
#define AS3935_REG_DISTANCE         (0x07)
#define AS3935_REG_DISP_LCO         (0x08)
#define AS3935_REG_DISP_SRCO        (0x08)
#define AS3935_REG_DISP_TRCO        (0x08)
#define AS3935_REG_TUN_CAP          (0x08)
#define AS3935_REG_LDLUT1           (0x09)
#define AS3935_REG_LDLUT2           (0x0A)
#define AS3935_REG_LDLUT3           (0x0B)
#define AS3935_REG_LDLUT4           (0x0C)
#define AS3935_REG_LDLUT5           (0x0D)
#define AS3935_REG_LDLUT6           (0x0E)
#define AS3935_REG_LDLUT7           (0x0F)
#define AS3935_REG_LDLUT8           (0x10)
#define AS3935_REG_LDLUT9           (0x11)
#define AS3935_REG_LDLUT10          (0x12)
#define AS3935_REG_LDLUT11          (0x13)
#define AS3935_REG_LDLUT12          (0x14)
#define AS3935_REG_LDLUT13          (0x15)
#define AS3935_REG_LDLUT14          (0x16)
#define AS3935_REG_LDLUT15          (0x17)
#define AS3935_REG_LDLUT16          (0x18)
#define AS3935_REG_LDLUT17          (0x19)
#define AS3935_REG_LDLUT18          (0x1A)
#define AS3935_REG_LDLUT19          (0x1B)
#define AS3935_REG_LDLUT20          (0x1C)
#define AS3935_REG_LDLUT21          (0x1D)
#define AS3935_REG_LDLUT22          (0x1E)
#define AS3935_REG_LDLUT23          (0x1F)
#define AS3935_REG_LDLUT24          (0x20)
#define AS3935_REG_LDLUT25          (0x21)
#define AS3935_REG_LDLUT26          (0x22)
#define AS3935_REG_LDLUT27          (0x23)
#define AS3935_REG_LDLUT28          (0x24)
#define AS3935_REG_LDLUT29          (0x25)
#define AS3935_REG_LDLUT30          (0x26)
#define AS3935_REG_LDLUT31          (0x27)
#define AS3935_REG_LDLUT32          (0x28)
#define AS3935_REG_LDLUT33          (0x29)
#define AS3935_REG_LDLUT34          (0x2A)
#define AS3935_REG_LDLUT35          (0x2B)
#define AS3935_REG_LDLUT36          (0x2C)
#define AS3935_REG_LDLUT37          (0x2D)
#define AS3935_REG_LDLUT38          (0x2E)
#define AS3935_REG_LDLUT39          (0x2F)
#define AS3935_REG_LDLUT40          (0x30)
#define AS3935_REG_LDLUT41          (0x31)
#define AS3935_REG_LDLUT42          (0x32)
//
#define AS3935_REG_PRESET_DEFAULT   (0x3C)
#define AS3935_REG_CALIB_RCO        (0X3D)

///////////////////////////////////////////////////////////////////////////////////////////////////
//
// Write a byte to the device
//
void write8(uint8_t reg, uint8_t val) {
  int err;
  for (err = 1 ; err ; err = 0) {
    if (!i2c_start(AS3935_ADDRESS | I2C_WRITE)) break;
    if (!i2c_write(reg)) break;
    if (!i2c_write(val)) break;
  }
  i2c_stop();
  //  if (err) emitln("error writing to device");
}

//
// Read a byte from the device
//
uint8_t read8(uint8_t reg) {
  int err;
  uint8_t val = 0;
  for (err = 1 ; err ; err = 0) {
    if (!i2c_start(AS3935_ADDRESS | I2C_WRITE)) break;
    if (!i2c_write(reg)) break;
    if (!i2c_rep_start(AS3935_ADDRESS | I2C_READ)) break;
    val = i2c_read(true);
  }
  i2c_stop();
  //  if (err) emitln("error reading from device");
  return (val);
}

//
// Interrupt service routine
//
volatile unsigned long event[] = {0, 0, 0, 0, 0};
ISR (PCINT0_vect) {
  int8_t val;
  digitalWrite(13, HIGH);
  event[4] = 1;
  if (digitalRead(INT_PIN)) {
    delayMicroseconds(2000);
    val = read8(AS3935_REG_INT);  // read the INT register
    if (val & 0x01) event[0] = 1; // noise is too high
    if (val & 0x02) event[1] = 1; // EM disturbance
    if (val & 0x04) event[2] = 1; // lightning
    if (val & 0x08) event[3] = 1; // updated distance to storm front
  }
  digitalWrite(13, LOW);
}

void run() {
  if (event[0]) {
    emit(millis()); emitln(": Noise is too high");
    event[0] = 0;
  }
  if (event[1]) {
    emit(millis()); emitln(": EM disturbance");
    event[1] = 0;
  }
  if (event[2]) {
    emit(millis()); emitln(": Lightening");
    event[2] = 0;
  }
  if (event[3]) {
    emit(millis()); emitln(": Updated distance");
    event[3] = 0;
  }
  if (event[4]) {
    emit(millis()); emitln(": OPI (Other People's Interrupts (for testing only))");
    event[4] = 0;
  }
}

///////////////////////////////////////////////////////////////////////////////////////////////////

void setup() {
  // Setup the terminal
#ifdef YUN_CONSOLE
  Bridge.begin();
  Console.begin();
  while (!Console);
#else
  Serial.begin(115200);
  while (!Serial);
#endif
  emitln(">Hello MOD-1016v6!");

  // Initialize SoftI2C
  i2c_init();
  // check for the device
  if (!i2c_start(AS3935_ADDRESS | I2C_WRITE))
    emitln(F("Device didn't respond"));
  i2c_stop();

  delay(10); // settle
  write8(AS3935_REG_TUN_CAP, AS3935_TUNE); // set tuning capacitor
  delay(2);
  write8(AS3935_REG_CALIB_RCO, AS3935_DIRECT_COMMAND_BYTE); // calibrate RC0
  write8(AS3935_REG_DISP_TRCO, 0x10); // calibrate TRC0
  delay(2);
  write8(AS3935_REG_DISP_TRCO, 0x00);
  write8(AS3935_REG_AFE_GB, AS3935_AFE_GB_INDOOR); // set gain
  //
  // Enable pin change interrupt
  // http://playground.arduino.cc/Main/PinChangeInterrupt
  //
  pinMode(INT_PIN, INPUT);
  *digitalPinToPCMSK(INT_PIN) |= bit (digitalPinToPCMSKbit(INT_PIN)); // enable pin
  PCIFR  |= bit (digitalPinToPCICRbit(INT_PIN)); // clear any outstanding interrupt
  PCICR  |= bit (digitalPinToPCICRbit(INT_PIN)); // enable interrupt for the group

  pinMode(13, OUTPUT);
  emitln(">Sanity check");
  emit("AFE_GB:    "); emitb(read8(0x00) >> 1);         emit(" (should be    10010)"); emitln();
  emit("LDLUT1: ");    emitb(read8(AS3935_REG_LDLUT1)); emit(" (should be 10101101)"); emitln();
  emitln(">Listening");
}

///////////////////////////////////////////////////////////////////////////////////////////////////

void loop() {
  run();
  delay(100);
}